buildscript{
    repositories{
        mavenCentral()
        maven{
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven{
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies{
        classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
    }
}

apply from: 'config.gradle'

apply plugin: 'scala'
apply plugin: 'forge'

version = mod_version
group = 'net.anti44.jsonrecipes'
archivesBaseName = 'JsonRecipes'

repositories{
    mavenCentral()
    repos.each{ maven{ name = it[0]; url = it[1] } }
}

dependencies{ deps.each{ compile(it) } }

minecraft{
    version = minecraft_version + '-' + forge_version
    runDir = '.minecraft'
    replace '@VERSION@' : project.version
}

ext.mf = manifest{ attributes('Created-By' : 'anti344') }

jar{
    classifier = 'universal'
    from sourceSets.api.output
    manifest.from mf
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.api.allSource
    classifier = 'src'
    manifest.from mf
}

task devJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.api.output
    classifier = 'dev'
    manifest.from mf
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    classifier = 'api'
    manifest.from mf
}

[srcJar, devJar, apiJar].each{artifacts.archives(it)}

processResources{
    inputs.property('version', project.version)
    inputs.property('mcversion', project.minecraft.version)
    from(sourceSets.main.resources.srcDirs){
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs){ exclude 'mcmod.info' }
    from('/'){ include 'COPYING', 'COPYING.LESSER' }
}